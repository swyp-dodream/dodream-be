plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'swyp'
version = '0.1.1-SNAPSHOT'
description = 'dodream'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    
    // Security & OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    
    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Swagger (OpenAPI 3.0) - Spring Boot 3.2.5 호환 버전
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    
    // Qdrant 벡터 데이터베이스 (REST API 사용)
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    // Elasticsearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'
    
    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'  // 테스트용 인메모리 DB
}

tasks.named('test') {
    useJUnitPlatform()
}

// .env 파일에서 환경변수 자동 로드
tasks.named('bootRun') {
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            // 주석(#)과 빈 줄 무시
            if (!line.trim().startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)
                environment key.trim(), value.trim()
            }
        }
    }
}
